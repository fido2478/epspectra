%################################
%   P_MOD1to2 module 
%################################

module P_MOD1to2:

input S_Mark_from_Up;%{set timer on}%
input Ack_From_Down1;%{for down module1}%
input Ack_From_Down2;%{for down module2}%
output E_Mark_to_Down;%{notify my mark finished to down}%
input S_Compute_from_Up;%{trigger my compute method}%
output E_Compute_to_Down;%{trigger compute method of downstream}%
output Ack_to_Up;%{inform the upstream of finishing computing}%
input snooping;
output sig_on; %{trigger TIMER on the upstream}%

%
%  body part
%

var INI_FLAG:=true:boolean in
signal switch_on, switch_off in
   [
      loop
	 if INI_FLAG then
	    await S_Mark_from_Up;
	    INI_FLAG:=false
	 else
	    run WAIT_SIG2[signal S_Mark_from_Up/IN1;
			  signal Ack_From_Down1/IN2;
			  signal Ack_From_Down2/IN3]
	 end if;
	 emit switch_on;
	 run MARK_DATA;
	 [
	 emit E_Mark_to_Down
         ||
	 emit switch_off
         ];
	 await immediate S_Compute_from_Up;
	 run COMPUTE_DATA2;
	 run TIMER[signal snooping/set_on];
	 [
	 emit Ack_to_Up
         ||
	 emit E_Compute_to_Down
         ]
      end loop
   ||
      run SWITCH[signal switch_on/On;
		 signal switch_off/Off;
		 signal sig_on/On_Signal];
   ]
end signal
end var
end module
